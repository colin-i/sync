
#aftercalli

#A structure
function arg(sv pbuffer,sd outfile)
	charx a#1;call get_char(pbuffer,#a)
	if a=(Xfile_arg_number)
		call number(pbuffer,outfile)
	elseif a=(Xfile_arg_varfn)
		call varfunc(pbuffer,outfile)
	elseif a=(Xfile_arg_string)
		datax sz#1;vstrx pointer#1
		call get_string(pbuffer,#sz,#pointer)
		call writeq(outfile)
		sd term;set term pointer;add term sz
		sd start;set start pointer
		while pointer!=term
			char chars="\n\r\t"
			char letters={asciin,asciir,asciit}
			ss r;set r strchr(#chars,pointer#)
			if r!=(NULL)
				sd c;set c pointer;sub c start
				if c!=0
					call write(outfile,start,c)
				end
				char bs=asciibs;char d#1
				sub r #chars
				add r #letters
				set d r#
				call write(outfile,#bs,2)
				inc pointer
				set start pointer
				continue
			end
			inc pointer
		end
		sub pointer start
		if pointer!=0
			call write(outfile,start,pointer)
		end
		call writeq(outfile)
	else #Xfile_arg_call
		callg cll(pbuffer,outfile)
	end
end

#C structure
function cal(sv pbuffer,sd outfile)
	char arg_call#1;call get_char(pbuffer,#arg_call)
	datax sz#1;valuex pointer#1
	call get_string(pbuffer,#sz,#pointer)
	callg write(outfile,pointer,sz)
end

#CL structure
function cll(sv pbuffer,sd outfile)
	call cal(pbuffer,outfile)
	char a=asciiparenthesisleft
	call write(outfile,#a,1)
	datax args#1;call get_data(pbuffer,#args)
	if args>=^1
	#need to reverse the push order
		set loop.directwrite (FALSE)
		set mems.carg_size 0
		set mems.cargx_size 0

		call carg_stamp()
		call arg(pbuffer,#mems.carg)
		call carg_stamp()
		sd n=1;while n!=args
			call arg(pbuffer,#mems.carg)
			call carg_stamp()
			inc n
		end
		set loop.directwrite (TRUE)
		call carg_resolve(outfile)
	end
	char b=asciiparenthesisright
	callg write(outfile,#b,1)
end
function carg_stamp()
	sd r;set r mwrite(#mems.carg_size,:,#mems.cargx)
	if r!=1
		call aftercallenable()
	end
end
function carg_resolve(sd outfile)
	sv cursor;set cursor mems.cargx_size
	add cursor mems.cargx
	decst cursor
	sd right;set right cursor#
	decst cursor
	call carg_resolve_one(cursor#,right,outfile)
	while mems.cargx!=cursor
		call writec(outfile)
		set right cursor#
		decst cursor
		call carg_resolve_one(cursor#,right,outfile)
	end
end
function carg_resolve_one(sv left,sv right,sd outfile)
	sub right left
	add left mems.carg
	callg write(outfile,left,right)
end

#CD structure
function cond(sv pbuffer,sd outfile)
	call arg(pbuffer,outfile)
	charx cnd#1;call get_char(pbuffer,#cnd)
	call arg(pbuffer,outfile)
	char a=asciicurlyleft
	callg writen(outfile,#a,1)
end

#DoT
function dot(sv pbuffer,sd outfile)
	datax sz#1;valuex pointer#1
	call get_string(pbuffer,#sz,#pointer)
	callg var(pbuffer,outfile)
end

#V structure
function var(sv pbuffer,sd outfile)
	datax sz#1;valuex pointer#1
	call get_string(pbuffer,#sz,#pointer)
	call write(outfile,pointer,sz)
	charx suffix#1;call get_char(pbuffer,#suffix)
	if suffix=(Xfile_suffix_true)
		charx cast#1;call get_char(pbuffer,#cast)
	end
	#Xfile_suffix_no Xfile_suffix_notasuffix
end

#VF structure
function varfunc(sv pbuffer,sd outfile)
	charx prefix#1;call get_char(pbuffer,#prefix)
	charx colon#1;call get_char(pbuffer,#colon)
	if colon=(Xfile_arg_varfn_colon_yes)
		charx dt#1;call get_char(pbuffer,#dt)
		if dt=(Xfile_arg_varfn_dot_yes)
			datax szf#1;valuex func#1
			call get_string(pbuffer,#szf,#func)
		end
		datax szv#1;valuex vr#1
		call get_string(pbuffer,#szv,#vr)
		charx suffix#1;call get_char(pbuffer,#suffix)
	end
	charx d#1;call get_char(pbuffer,#d)
	if d=(Xfile_arg_varfn_dot_no)
		call var(pbuffer,outfile)
	else
		callg dot(pbuffer,outfile)
	end
end
