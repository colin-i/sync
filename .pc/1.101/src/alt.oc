
format elfobj64

include "../include/prog.h"

importx "strrchr" strrchr
importx "strcpy" strcpy
importx "memcpy" memcpy
importx "sprintf" sprintf
importx "access" access
importx "unlink" unlink
importx "rmdir" rmdir

import "swf_sprite_frames_pointer" swf_sprite_frames_pointer
import "word_arg_to_stack" word_arg_to_stack
import "mem_free" mem_free

function alt_join(vstr concat_point)
	value folder=NULL
	add concat_point folder
	call strcpy(concat_point,".dbg")
	add concat_point 4;set concat_point# (path_separator)
	inc concat_point
end
function alt_free()
	if alt_join.folder!=(NULL)
		set alt_join.concat_point# (Nullchar)
		call rmdir(alt_join.folder)
		call mem_free(#alt_join.folder)
	end
end

import "ainit" ainit

importx "strlen" strlen

#aftercallimport ebool

import "memalloc" memalloc
import "error" error
import "len_of" len_of

function alt_init(sd flags,sd path)
	if alt_join.folder!=(NULL)
		call error("swf started, alt not closed")
	end
	and flags (debug_alt)
	if flags=(debug_alt)
		sd size;setcall size strlen(path)
		sd r;set r strrchr(path,(Period))
		if r!=(NULL)
			sub r path
			set size r
		end
		sd finalsize;set finalsize len_of(size,4+ext_str_len) #ext+...

		setcall alt_join.folder memalloc(finalsize)
		call memcpy(alt_join.folder,path,size)
		call alt_join(size)
		ret
	end
end

import "file_get_content" file_get_content

function alt_action(sd orig_action,sd preid)
	call alt_action_base(orig_action,preid,(TRUE))
end
function alt_action_base(sd orig_action,sd preid,sd has_frames)
	if alt_join.folder!=(NULL)
		whiletrue
			if has_frames=(TRUE)
				sd ai;set ai ainit()
				if ai=(FALSE)
					sw p
					#if preid!=0
					set p swf_sprite_frames_pointer(preid)
					#else #set p swf_frames_pointer()#end
					sd f;set f word_arg_to_stack(p)
					inc f  ##index 0 is frame 1 at ffdec
					call sprintf(alt_join.concat_point,"%u_%u",preid,f)
					break
				end
			end
			call sprintf(alt_join.concat_point,"%u",preid)
			break
		end
		datax int#1;set int access(alt_join.folder,(R_OK))
		if int=0
			sd mem
			set mem file_get_content(alt_join.folder,(NULL))
			call unlink(alt_join.folder) #if there are two action() at one frame, the second one must not see this
			return mem
		end
		return (NULL)
	end
	return orig_action
end
